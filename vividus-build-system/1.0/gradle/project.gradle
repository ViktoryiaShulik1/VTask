/*
 * Copyright 2019 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

rootProject.wrapper.gradleVersion = '6.0.1'

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath(group: 'pl.allegro.tech.build', name: 'axion-release-plugin', version: '1.10.2')
    }
}
apply plugin: pl.allegro.tech.build.axion.release.ReleasePlugin

def customVersionPrefix = project.findProperty('versionPrefix') ?: ''
scmVersion {
    tag {
        if (customVersionPrefix.length() > 0) {
            prefix = 'releases/' + customVersionPrefix
        }
    }
}

def customPrefixWithSeparator = customVersionPrefix.length() > 0 ? customVersionPrefix + '-' : ''


allprojects {
    buildDir = file(System.env.OUTPUT_ROOT ? "${System.env.OUTPUT_ROOT}/${project.name}" : 'output')
    project.version = new File(scmVersion.repository.directory, '.git').exists() ?
            customPrefixWithSeparator + scmVersion.version : 'local-SNAPSHOT'
}

repositories {
    mavenCentral()
}
